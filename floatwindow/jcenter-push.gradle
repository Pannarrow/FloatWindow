apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def packageName = "cn.sunline.component.externalBackup"
def moduleName = "FloatWindow"
def moduleVersion ="1.0.9"

def libraryVersion = project.hasProperty('libraryVersion') ? libraryVersion : moduleVersion

publishing {
    publications {
        aar(MavenPublication) {
            groupId packageName
            version libraryVersion
            artifactId moduleName

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            // generate POM file
            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.group != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope','compile')
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'http://10.22.30.19:8082/artifactory'
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = 'libs-release-local'

            username = "admin"
            password = "sunline~*%154%#@%"
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true

            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}
